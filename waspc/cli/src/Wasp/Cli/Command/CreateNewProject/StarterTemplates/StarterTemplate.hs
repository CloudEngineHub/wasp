module Wasp.Cli.Command.CreateNewProject.StarterTemplates.StarterTemplate
  ( StarterTemplate (..),
    DirBasedTemplateMetadata (..),
    TemplateMetadata (..),
    StartingInstructionsBuilder,
    getTemplateStartingInstructions,
  )
where

import StrongPath (Dir', Path', Rel')
import qualified Wasp.Cli.GithubRepo as GhRepo
import qualified Wasp.Cli.Interactive as Interactive

data StarterTemplate
  = -- | Template from a Github repo.
    GhRepoStarterTemplate !GhRepo.GithubRepoRef !DirBasedTemplateMetadata
  | -- | Template from a disk, that comes bundled with wasp CLI.
    LocalStarterTemplate !DirBasedTemplateMetadata
  | -- | Template that will be dynamically generated by Wasp AI based on user's input.
    -- TODO: Add notion of AI that will be used, as a field. For now just data WaspAppAiGenerator = WaspAI.
    --   Then also update aiGeneratedStarterTemplate to be called waspAiGeneratedStarterTemplate and to use that value.
    AiGeneratedStarterTemplate !TemplateMetadata

data DirBasedTemplateMetadata = DirBasedTemplateMetadata
  { _name :: !String,
    _path :: !(Path' Rel' Dir'), -- Path to a directory containing template files.
    _description :: !String,
    _buildStartingInstructions :: !StartingInstructionsBuilder
  }

-- TODO: Have DirBasedTemplateMetadata use TemplateMetadata inside it.
--   Maybe even go step further and use typeclass instead? It should give me name, description, starting instructions.
--   class IsStarterTemplate
data TemplateMetadata = TemplateMetadata
  { _tmplName :: !String,
    _tmplDescription :: !String,
    _tmplBuildStartingInstructions :: !StartingInstructionsBuilder
  }

instance Show StarterTemplate where
  show (GhRepoStarterTemplate _ metadata) = _name metadata
  show (LocalStarterTemplate metadata) = _name metadata
  show (AiGeneratedStarterTemplate metadata) = _tmplName metadata

instance Interactive.IsOption StarterTemplate where
  showOption = show

  showOptionDescription (GhRepoStarterTemplate _ metadata) = Just $ _description metadata
  showOptionDescription (LocalStarterTemplate metadata) = Just $ _description metadata
  showOptionDescription (AiGeneratedStarterTemplate metadata) = Just $ _tmplDescription metadata

-- TODO: This doesn't seem to be documented well, what is this first String? Maybe project path? Why is it not strong path then?
type StartingInstructionsBuilder = String -> String

-- | Returns instructions for running the newly created (from the template) Wasp project.
-- Instructions assume that user is positioned right next to the just created project directory,
-- whose name is provided via projectDirName.
getTemplateStartingInstructions :: String -> StarterTemplate -> String
getTemplateStartingInstructions projectDirName = \case
  GhRepoStarterTemplate _ metadata -> _buildStartingInstructions metadata projectDirName
  LocalStarterTemplate metadata -> _buildStartingInstructions metadata projectDirName
  AiGeneratedStarterTemplate metadata -> _tmplBuildStartingInstructions metadata projectDirName
